plugins {
    id "com.github.johnrengelman.shadow" version "8.1.0" apply false
    id "net.kyori.blossom" version "1.3.1" apply false
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    group = "io.github.lxgaming"

    repositories {
        mavenCentral()
        maven {
            name = "md-5"
            url = "https://repo.md-5.net/content/repositories/releases"
        }
        maven {
            name = "mojang"
            url = "https://libraries.minecraft.net"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots"
        }
        maven {
            name = "velocity"
            url = "https://nexus.velocitypowered.com/repository/maven-public"
        }
    }

    dependencies {
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        failOnError = false
    }

    publishing {
        publications {
            mavenPublication(MavenPublication) {
                groupId group
                artifactId archivesBaseName
                version version
                pom {
                    name = "Location"
                    description = "Player location tracking for Waterfall and Velocity"
                    url = "https://github.com/LXGaming/Location"
                    developers {
                        developer {
                            id = "lxgaming"
                            name = "LXGaming"
                        }
                    }
                    issueManagement {
                        system = "GitHub Issues"
                        url = "https://github.com/LXGaming/Location/issues"
                    }
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    scm {
                        connection = "scm:git:https://github.com/LXGaming/Location.git"
                        developerConnection = "scm:git:https://github.com/LXGaming/Location.git"
                        url = "https://github.com/LXGaming/Location"
                    }
                }
            }
        }
    }

    signing {
        if (project.hasProperty("signingKey") && project.hasProperty("signingPassword")) {
            useInMemoryPgpKeys(project.property("signingKey"), project.property("signingPassword"))
        }

        sign publishing.publications
    }

    task signJar {
        doFirst {
            if (!project.hasProperty("signing.keyStorePath") || !project.hasProperty("signing.secretKeyRingFile")) {
                project.logger.warn("========== [WARNING] ==========")
                project.logger.warn("")
                project.logger.warn("   This build is not signed!   ")
                project.logger.warn("")
                project.logger.warn("========== [WARNING] ==========")
                throw new StopExecutionException()
            }
        }

        doLast {
            configurations.archives.allArtifacts.files.each {
                ant.signjar(
                        jar: it,
                        alias: project.property("signing.alias"),
                        storepass: project.property("signing.keyStorePassword"),
                        keystore: project.property("signing.keyStorePath"),
                        keypass: project.property("signing.keyStorePassword"),
                        preservelastmodified: project.property("signing.preserveLastModified"),
                        tsaurl: project.property("signing.timestampAuthority"),
                        digestalg: project.property("signing.digestAlgorithm")
                )
                project.logger.lifecycle("JAR Signed: ${it.name}")

                signing.sign(it)
                project.logger.lifecycle("PGP Signed: ${it.name}")
            }
        }
    }
}