plugins {
    id "net.kyori.blossom"
}

archivesBaseName = "location-api"

blossom {
    replaceToken("@version@", version)
}

dependencies {
    api("com.google.guava:guava:${guavaVersion}")
    api("io.reactivex.rxjava3:rxjava:${rxjavaVersion}")
    api("net.kyori:adventure-api:${adventureVersion}")
    api("org.checkerframework:checker-qual:${checkerqualVersion}")
}

processResources {
    from("../LICENSE")
    rename("LICENSE", "LICENSE-Location")
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
        }
    }
    repositories {
        if (project.hasProperty("sonatypeUsername") && project.hasProperty("sonatypePassword")) {
            maven {
                name = "sonatype"
                url = version.contains("-SNAPSHOT") ? "https://s01.oss.sonatype.org/content/repositories/snapshots" : "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username project.property("sonatypeUsername")
                    password project.property("sonatypePassword")
                }
            }
        }
    }
}