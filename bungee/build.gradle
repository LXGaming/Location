plugins {
    id "com.github.johnrengelman.shadow"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "location-bungee"

configurations {
}

repositories {
}

dependencies {
    implementation(project(path: ":location-common")) {
        exclude(module: "brigadier")
        exclude(module: "gson")
        exclude(module: "guava")
        exclude(module: "netty-all")
        exclude(module: "slf4j-api")
    }
    compileOnly(fileTree(dir: "libs", include: ["*.jar"]))
    compileOnly("net.md-5:bungeecord-api:${bungeecordVersion}")
    implementation("net.kyori:adventure-platform-bungeecord:4.0.0-SNAPSHOT") {
        exclude(module: "gson")
    }
    compileOnly("org.slf4j:slf4j-api:${slf4jVersion}")
}

build {
    dependsOn("signJar")
}

compileJava {
    dependsOn(":location-common:build")
}

jar {
    enabled = false
    dependsOn("shadowJar")
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from (sourceSets.main.resources.srcDirs) {
        include("bungee.yml")
        expand("name": "Location", "version": version)
    }
}

shadowJar {
    archiveClassifier.set(null)
    relocate("net.kyori.adventure", "io.github.lxgaming.location.lib.adventure")
    relocate("net.kyori.examination", "io.github.lxgaming.location.lib.examination")
    relocate("org.checkerframework", "io.github.lxgaming.location.lib.checkerframework")
}